apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.jetbrains.dokka-android'

apply from: '../buildsystem/config.gradle'
apply from: 'release.gradle'

android {
    final config = project.ext.config
    compileSdkVersion config.compileSdkVersion
    buildToolsVersion config.buildToolsVersion
    defaultConfig {
        versionCode config.versionCode
        versionName config.versionName
        minSdkVersion config.minSdkVersion
        targetSdkVersion config.targetSdkVersion
    }
    compileOptions {
        sourceCompatibility config.javaCompatibility
        targetCompatibility config.javaCompatibility
    }
    lintOptions {
        abortOnError true
        absolutePaths true
        checkAllWarnings true
        htmlOutput = new File("$project.buildDir/lint.html")
        quiet false
        warningsAsErrors true
    }
    sourceSets.forEach {
        it.java.srcDirs = ["src/$it.name/kotlin"]
    }
}

repositories {
    jcenter()
}

dependencies {
    final deps = rootProject.ext.dependencies
    implementation deps.kotlinStdLib
    api deps.store
    testImplementation deps.kotlinTest
}
